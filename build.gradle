plugins {
    id 'dev.architectury.loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
    id "com.hypherionmc.modutils.modpublisher" version "2.1.6"
    id "co.uzzu.dotenv.gradle" version "4.0.0"
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_name
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }

    maven {
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }

    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.rtyley"
        }
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

loom {
    silentMojangMappingsLicense()
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.officialMojangMappings()
    neoForge "net.neoforged:neoforge:$project.neoforge_version"

    modImplementation "dev.latvian.mods:kubejs-neoforge:$kubejs_version"
    modImplementation "dev.latvian.mods:rhino:$rhino_version"
    modImplementation "curse.maven:projecte-226410:$projecte_version"
    modImplementation "dev.architectury:architectury-neoforge:$architectury_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {}
}

publisher {
    apiKeys {
        if (env.isPresent("CURSE_TOKEN")) curseforge env.CURSE_TOKEN.value
        if (env.isPresent("MODRINTH_TOKEN")) modrinth env.MODRINTH_TOKEN.value
    }

    setCurseID rootProject.curseforge_id
    setModrinthID rootProject.modrinth_id

    setVersionType "release"
    setDisplayName "[$rootProject.minecraft_version] ProjectE - KubeJS | $rootProject.mod_version"
    setGameVersions rootProject.minecraft_version
    setLoaders([project.name])
    setCurseEnvironment "both"
    setArtifact remapJar
    setVersion rootProject.minecraft_version
    setJavaVersions([java.targetCompatibility])
    setChangelog "Initiale Release"
}


