plugins {
    id 'base'
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.moddev' version "2.0.10-beta"
}

ext.ENV = System.getenv()
version = project.mod_version
group = project.maven_group

base.archivesName = project.archives_name

neoForge {
    version = project.neoforge_version

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.INFO
        }

        client {
            client()

            if (ENV.MC_CLIENT_ARGS) {
                programArguments.addAll(ENV.MC_CLIENT_ARGS.split(' '))
            }

            jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
            gameDirectory = file 'run'
        }

        server {
            server()
            programArgument("--nogui")
            gameDirectory = file 'run_server'
        }
    }
}

compileJava {
    options.encoding = "UTF-8"
    options.release.set(21)
}

java {
    sourceCompatibility = targetCompatibility = '21'
    withSourcesJar()
}


repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }


    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
        content {
            includeGroup "org.parchmentmc.data"
        }
    }

    maven {
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }

    maven {
        url 'https://jitpack.io'
        content {
            includeGroup "com.github.rtyley"
        }
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        url "https://maven.architectury.dev/"
        content {
            includeGroup "dev.architectury"
        }
    }
}


dependencies {
    api "dev.latvian.mods:kubejs-neoforge:$kubejs_version"

    implementation "dev.latvian.mods:rhino:$rhino_version"
    implementation "curse.maven:projecte-226410:$projecte_version"
    implementation "dev.architectury:architectury-neoforge:$architectury_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "kjsprojecte",
                "Specification-Vendor"    : "bobvarioa",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "bobvarioa",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}
